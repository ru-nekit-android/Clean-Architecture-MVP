apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply from: '../code_quality_tools/quality.gradle'

android {

    compileSdkVersion versions.androidCompileSdkVersion
    buildToolsVersion versions.androidBuildToolsVersion

    defaultConfig {

        applicationId versions.androidApplicationId
        minSdkVersion versions.androidMinSdkVersion
        targetSdkVersion versions.androidTargetSdkVersion
        versionCode versions.androidVersionCode
        versionName versions.androidVersionName

        buildConfigField "String", "API_URL", '"https://api.github.com/"'
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'

            // Enables code coverage with Jacoco only for instrumentation tests.
            // Coverage for JVM tests done by jacoco.gradle.
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //TODO: explore lint
    lintOptions {
        textReport true
        textOutput 'stdout'
        fatal 'UnusedResources'

        warningsAsErrors true
        abortOnError true // Fail early.

        disable 'GoogleAppIndexingWarning' // App does not have deep linking.
        //disable 'InvalidPackage' // Okio references java.nio that does not presented in Android SDK.
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    /*productFlavors {
        internal {
            applicationId 'ru.nekit.android.clean_architecture.internal'
        }
        production {
            applicationId 'ru.nekit.android.clean_architecture'
        }
    }*/
}

dependencies {

    compile project(':domain')
    compile project(':data')

    compile libraries.androidSupportv7
    compile libraries.androidDesign
    compile libraries.cardView
    compile libraries.recyclerView

    compile libraries.rxJava
    compile libraries.rxAndroid
    //compile libraries.rxBinding
    compile libraries.butterknife

    apt libraries.daggerCompiler
    compile libraries.dagger

    debugCompile debugLibryries.leakCanaryDebug
    debugCompile debugLibryries.lynx

    testCompile testLibraries.junit
    compile 'com.android.support:support-v4:23.2.1'
}

configurations.all {
    resolutionStrategy {
        // Force our version of support-annotations, we have a conflict between app and androidTest dependency resolution.
        force libraries.supportAnnotations
    }
}
