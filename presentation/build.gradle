apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'hu.supercluster.paperwork'
apply from: '../code_quality/quality.gradle'

android {
    signingConfigs {
        release {
            storeFile file(STORE_FILE)
            storePassword KEYSTORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }
    compileSdkVersion versions.androidCompileSdkVersion
    buildToolsVersion versions.androidBuildToolsVersion
    defaultConfig {

        applicationId versions.androidApplicationId
        minSdkVersion versions.androidMinSdkVersion
        targetSdkVersion versions.androidTargetSdkVersion
        versionCode versions.androidVersionCode
        versionName versions.androidVersionName

    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'

            // Enables code coverage with Jacoco only for instrumentation tests.
            // Coverage for JVM tests done by jacoco.gradle.
            testCoverageEnabled true
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

paperwork {
    set = [
            gitSha   : gitSha(),
            buildDate: buildTime("dd-MM-yyyy HH:MM:SS z", "UTC"),
            API_URL  : "https://api.github.com/"
    ]
}

dependencies {

    compile project(':domain')
    compile project(':data')

    compile libraries.androidSupportv7
    compile libraries.androidDesign
    compile libraries.cardView
    compile libraries.recyclerView
    compile libraries.rxJava
    compile libraries.rxAndroid
    compile libraries.butterknife
    apt libraries.daggerCompiler
    compile libraries.dagger
    compile libraries.paperwork

    debugCompile debugLibryries.leakCanaryDebug
    debugCompile debugLibryries.lynx

    testApt libraries.daggerCompiler
    testCompile testLibraries.junit
    testCompile testLibraries.robolectric
    testCompile testLibraries.assertJ
    testCompile testLibraries.okHttpMockWebServer
    testCompile testLibraries.mockitoCore
}